repositories {
    maven { url "https://soot-build.cs.uni-paderborn.de/nexus/repository/soot-release/" }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom integrationTestCompile, testRuntime
}

dependencies {
    compile project(":pipeline")

    testCompile group: "com.nhaarman", name: "mockito-kotlin", version: mockitoKotlinVersion

    integrationTestCompile project(":models:jimple-library-usage-graph")
    integrationTestCompile group: "ca.mcgill.sable", name: "soot", version: sootVersion
}

// Integration test src
sourceSets {
    integrationTest {
        kotlin.srcDirs file("src/integration-test/kotlin")
        resources.srcDirs file("src/integration-test/resources")

        compileClasspath += sourceSets.main.output
        compileClasspath += configurations.compileOnly
        compileClasspath += configurations.testCompileOnly
        runtimeClasspath += compileClasspath
    }
}

task integrationTest(type: Test, description: "Runs the integration tests.", group: "Verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    jacoco {
        append = false
    }

    useJUnitPlatform() {
        includeEngines "spek"
    }

    jacocoTestReport {
        executionData file("$buildDir/jacoco/integration-test.exec")
    }
}

integrationTest.mustRunAfter test
check.dependsOn integrationTest
