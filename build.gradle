buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessVersion"
    }
}


// Shared config
allprojects {
    /// Plugins
    // Compilation
    apply plugin: "java"
    apply plugin: "kotlin"

    // Documentation
    apply plugin: "org.jetbrains.dokka"

    // Static analysis
    apply plugin: "com.diffplug.gradle.spotless"
    apply plugin: "jacoco"


    /// Dependencies
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        compile group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: kotlinVersion
        compile group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: kotlinVersion
        implementation group: "io.github.microutils", name: "kotlin-logging", version: kotlinLoggingVersion

        testCompile group: "org.assertj", name: "assertj-core", version: assertjVersion
        testCompile group: "org.jetbrains.spek", name: "spek-api", version: spekVersion
        testRuntime group: "org.jetbrains.spek", name: "spek-junit-platform-engine", version: spekVersion
        testCompile group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion
    }


    /// Configuration
    // Kotlin
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    // JUnit / Spek
    test {
        testLogging {
            exceptionFormat = "full"
        }

        useJUnitPlatform() {
            includeEngines "junit-jupiter", "spek"
        }
    }

    // Spotless
    spotless {
        kotlin {
            ktlint()
        }
    }

    // Jacoco
    test {
        jacoco {
            append = true
            destinationFile = file("$buildDir/jacoco/test.exec")
        }
    }

    jacoco {
        toolVersion = jacocoVersion
    }

    jacocoTestReport {
        additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
        sourceDirectories = files(sourceSets.main.allSource.srcDirs)
        classDirectories = files(sourceSets.main.output)
        executionData = files("$buildDir/jacoco/test.exec")

        reports {
            csv.enabled = false
            html.enabled = true
            xml.enabled = true
        }
    }
}


// Global config
apply from: "gradle/detekt.gradle"

// Jacoco report combining all reports
task jacocoRootReport(type: JacocoReport) {
    dependsOn = allprojects.test
    additionalSourceDirs = files(allprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(allprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(allprojects.sourceSets.main.output)
    executionData = files(allprojects.jacocoTestReport.executionData)

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }

    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }

    onlyIf = { true }
}
